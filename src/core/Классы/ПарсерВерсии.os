Перем СтрокаДляРазбора;
Перем Индекс;

Перем ТипКонецТекста Экспорт;
Перем ТипЧисло Экспорт;
Перем ТипТекст Экспорт;
Перем ТипРазделитель Экспорт;
Перем ТипОшибка Экспорт;

Перем ДиапазонЧислоНачало;
Перем ДиапазонЧислоКонец;

Перем ДиапазонЗаглавныеБуквыНачало;
Перем ДиапазонЗаглавныеБуквыКонец;
Перем ДиапазонСтрочныеБуквыНачало;
Перем ДиапазонСтрочныеБуквыКонец;
Перем Дефис;
Перем Плюс;
Перем Точка;

Процедура ПриСозданииОбъекта(Знач ВерсияСтрокой)
    СтрокаДляРазбора = ВерсияСтрокой;
    Если ПустаяСтрока(СтрокаДляРазбора) Тогда
        Индекс = -1;
    Иначе
        Индекс = 1;
    КонецЕсли;
КонецПроцедуры

Функция Токен(Знач Значение, Знач Тип)
    Возврат Новый Структура("Тип, Значение", Тип, Значение);
КонецФункции

Функция Следующий() Экспорт
    
    Если Индекс = -1 Тогда
        Возврат Токен("", ТипКонецТекста);
    КонецЕсли;
    
    ТипТокена = ТипЧисло;
    НачалоЗначения = Индекс;
    ЧислоСимволов = 0;
    
    Пока Истина Цикл
        
        Символ = Сред(СтрокаДляРазбора, Индекс, 1);
        КодСимвола = КодСимвола(Символ);
        Если ПустаяСтрока(Символ) Тогда
            Если ЧислоСимволов = 0 Тогда
                // Это последний прочитанный токен
                Индекс = -1;
                Возврат Токен("", ТипКонецТекста);
            КонецЕсли;
            Прервать;
        КонецЕсли;
        
        // Инлайн, чтобы было побыстрее и не тратило время на вызов метода
        Если (КодСимвола >= ДиапазонЗаглавныеБуквыНачало И КодСимвола <= ДиапазонЗаглавныеБуквыКонец) ИЛИ
            (КодСимвола >= ДиапазонСтрочныеБуквыНачало И КодСимвола <= ДиапазонСтрочныеБуквыКонец) Тогда
            ТипТокена = ТипТекст;
        ИначеЕсли КодСимвола >= ДиапазонЧислоНачало И КодСимвола <= ДиапазонЧислоКонец Тогда
            // обработка числа
        ИначеЕсли КодСимвола = Точка ИЛИ КодСимвола = Дефис ИЛИ КодСимвола = Плюс Тогда
            // разделитель
            Прервать;
        Иначе
            // ошибка
            Позиция = Индекс;
            Индекс = -1; // блокировка парсера
            Возврат Токен(СтрШаблон("Недопустимый символ '%1' в позиции %2", Символ, Позиция), ТипОшибка);
        КонецЕсли;
        
        Индекс = Индекс + 1;
        ЧислоСимволов = ЧислоСимволов + 1;
    КонецЦикла;
    
    Если ЧислоСимволов = 0 Тогда
        // Это разделитель
        Значение =  Сред(СтрокаДляРазбора, Индекс, 1);
        Индекс = Индекс + 1;
        Возврат Токен(Значение, ТипРазделитель);
    КонецЕсли;

    Значение = Сред(СтрокаДляРазбора, НачалоЗначения, ЧислоСимволов);

    Возврат Токен(Значение, ТипТокена);

КонецФункции

ТипКонецТекста = 0;
ТипЧисло = 1;
ТипТекст = 2;
ТипРазделитель = 3;
ТипОшибка = 4;

ДиапазонЧислоНачало = 48;
ДиапазонЧислоКонец = 57;
ДиапазонЗаглавныеБуквыНачало = 65;
ДиапазонЗаглавныеБуквыКонец = 90;
ДиапазонСтрочныеБуквыНачало = 97;
ДиапазонСтрочныеБуквыКонец = 122;
Дефис = КодСимвола("-");
Плюс = КодСимвола("+");
Точка = КодСимвола(".");